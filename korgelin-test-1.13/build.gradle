buildscript {
    ext.kotlin_version = '1.3.31'
    apply from: "../buildSrc/config.gradle.kts"
    repositories {
        jcenter()
        maven {
            name "forge"
            url "http://files.minecraftforge.net/maven/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "net.minecraftforge.gradle"
apply from: "../buildSrc/config.gradle.kts"

group 'net.toliner.korgelin'
version = modVersion + '-' + kotlinVersion
archivesBaseName = "Korgelin-test-1.13"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings = mcp_mappings

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                KorgelinTest {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                KorgelinTest {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    mod
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
    implementation project(":../1.13")
    mod project(":../1.13")
    // project dependency do not run task "shadowJar"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: kotlinVersion
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: kotlinVersion
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: kotlinVersion
    implementation group: "org.jetbrains", name: "annotations", version: annotationsVersion
    implementation group: "org.jetbrains.kotlinx", name: "kotlinx-coroutines-core", version: coroutineVersion
    implementation group: "org.jetbrains.kotlinx", name: "kotlinx-coroutines-jdk8", version: coroutineVersion
    implementation group: "org.jetbrains.kotlinx", name: "kotlinx-serialization-runtime", version: serializationVersion
}

configurations {
    runtime.exclude group: "net.toliner.korgelin"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task installMods(type: Copy, dependsOn: "deinstallMods") {
    from { configurations.mod }
    include "**/Korgelin*"
    into file("run/mods")
}

task deinstallMods(type: Delete) {
    delete fileTree(dir: "run/mods", include: "*.jar")
}

project.afterEvaluate {
    project.tasks['prepareRuns'].dependsOn(project.tasks['installMods'])
}
